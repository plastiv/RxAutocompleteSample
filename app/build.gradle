apply plugin: 'com.android.application'

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    defaultConfig {
        applicationId "com.github.plastiv.rxautocompletesample"
        minSdkVersion 21
        targetSdkVersion 22
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        // hide google places api key with gradle property
        buildConfigField "String", "GOOGLE_PLACES_API", propertyOrDefault('googleApiKey', '"provideyourownkey"')
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    packagingOptions {
        exclude 'LICENSE.txt'
    }

    testOptions {
        // provide TextUtils implementation
        unitTests.returnDefaultValues = true
    }
}

dependencies {
    // main RxJava dependency
    compile "io.reactivex:rxjava:1.0.10"
    // android RxJava extension
    compile "io.reactivex:rxandroid:0.24.0"
    // retrofit dependencies to connect with google places api
    compile "com.squareup.retrofit:retrofit:1.9.0"
    compile "com.squareup.okhttp:okhttp:2.3.0"

    // unit test dependencies
    testCompile "junit:junit:4.12"
    testCompile "org.mockito:mockito-core:1.9.5"
    testCompile "com.google.truth:truth:0.25"

    // ui test dependencies
    androidTestCompile "com.android.support.test:runner:0.2"
    androidTestCompile "com.android.support.test:rules:0.2"
    androidTestCompile "com.android.support.test.espresso:espresso-core:2.1"
    // mockito
    androidTestCompile 'org.mockito:mockito-core:1.9.5'
    androidTestCompile 'com.google.dexmaker:dexmaker:1.2' // required by Mockito
    androidTestCompile 'com.google.dexmaker:dexmaker-mockito:1.2' // required by Mockito
}

// https://gradle.org/docs/current/userguide/tutorial_this_and_that.html#sec:gradle_properties_and_system_properties
def propertyOrDefault(propertyName, defaultValue) {
    return hasProperty(propertyName) ? property(propertyName) : defaultValue
}
