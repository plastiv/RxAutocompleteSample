apply plugin: 'com.android.application'

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    defaultConfig {
        applicationId "com.github.plastiv.rxautocompletesample"
        minSdkVersion 21
        targetSdkVersion 22
        versionCode 1
        versionName "1.0"
        // hide google places api key with gradle property
        buildConfigField "String", "GOOGLE_PLACES_API", propertyOrDefault('googleApiKey', '"provideyourownkey"')
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    testOptions {
        // provide TextUtils implementation
        unitTests.returnDefaultValues = true
    }
}

dependencies {
    // main RxJava dependency
    compile "io.reactivex:rxjava:1.0.9"
    // android RxJava extension
    compile "io.reactivex:rxandroid:0.24.0"
    // retrofit dependencies to connect with google places api
    compile "com.squareup.retrofit:retrofit:1.9.0"
    compile "com.squareup.okhttp:okhttp:2.3.0"

    testCompile "junit:junit:4.12"
    testCompile "org.mockito:mockito-core:1.9.5"
    testCompile "com.google.truth:truth:0.25"
}

// https://gradle.org/docs/current/userguide/tutorial_this_and_that.html#sec:gradle_properties_and_system_properties
def propertyOrDefault(propertyName, defaultValue) {
    return hasProperty(propertyName) ? property(propertyName) : defaultValue
}
